module(
    name = "jps_to_bazel",
    version = "0.1",
)

bazel_dep(name = "rules_java", version = "9.3.0")
bazel_dep(name = "rules_kotlin", version = "2.2.2")
archive_override(
    module_name = "rules_kotlin",
    sha256 = "1600acb76079b1356553a6dc2685f9304666d07383830c25efe3491763732e25",
    strip_prefix = "rules_kotlin-7da67b3a776d7785a5ece293ca771df7bc907965",
    url = "https://github.com/JetBrains/rules_kotlin/archive/7da67b3a776d7785a5ece293ca771df7bc907965.zip",
)
bazel_dep(name = "rules_jvm_external", version = "6.10")
single_version_override(
  module_name = "rules_jvm_external",
  version = '6.10',
  patches = [
    "rules-jvm-external-6.10-make-coursier-repo-targets-shell-free-and-hermetic.patch"
  ],
)

bazel_dep(name = "protobuf", version = "33.4")
single_version_override(
    module_name = "protobuf",
    version = "33.4",
    patch_strip = 1,
    patches = [
      "protobuf-33.4-reuse-win64-for-arm.patch",
    ],
)

intellij_version = "253.29346.138"

maven = use_extension("@rules_jvm_external//:extensions.bzl", "maven")
maven.install(
    name = "j2b_maven",
    artifacts = [
        "com.jetbrains.intellij.platform:jps-model-impl:" + intellij_version,
        "com.jetbrains.intellij.platform:build-scripts-downloader:" + intellij_version,
        "com.jetbrains.intellij.platform:jps-model-serialization:" + intellij_version,
        "org.jetbrains.kotlin:kotlin-jps-plugin:2.2.0",
        "org.jetbrains.teamcity:serviceMessages:2024.07",
    ],
    fail_if_repin_required = True,
    fetch_sources = True,
    lock_file = "//:maven_install.json",
    repositories = [
        "https://cache-redirector.jetbrains.com/repo1.maven.org/maven2",
        "https://cache-redirector.jetbrains.com/intellij-dependencies",
        "https://cache-redirector.jetbrains.com/download.jetbrains.com/teamcity-repository",
        "https://cache-redirector.jetbrains.com/www.jetbrains.com/intellij-repository/releases",
    ],
)
use_repo(maven, "j2b_maven")

bazel_dep(name = "rules_python", version = "1.7.0")

# Use custom remote_java_tools_windows with vc_redist .dll files due to https://github.com/bazelbuild/rules_java/issues/316
http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")
http_archive(
    name = "remote_java_tools_windows_with_vc_redist",
    sha256 = "9ec4e41ebbb2b7259eb14f1ad708e8ee60087f33515bdec4876c675205e6950f",
    url = "https://packages.jetbrains.team/files/p/ij/intellij-build-dependencies/rules-java/java_tools_windows-v13.18-with-vc_redist.zip",
)
override_repo(
    use_extension("@rules_java//java:extensions.bzl", "toolchains"),
    remote_java_tools_windows = "remote_java_tools_windows_with_vc_redist",
)
